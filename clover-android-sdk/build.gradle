/**
 * Copyright (C) 2016 Clover Network, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
group = 'com.clover.sdk'
version = '304'

apply from: file("${project.rootDir}/lib.gradle")

android {
  defaultConfig {
    multiDexEnabled true
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes.all {
    consumerProguardFiles 'proguard-rules.pro'
  }
}

dependencies {
  api files('lib/calc-jvm.jar')
  api 'org.jetbrains.kotlin:kotlin-stdlib:1.5.0'
  api 'org.jetbrains.kotlin:kotlin-stdlib-common:1.5.0'

  implementation "androidx.annotation:annotation:$ANDROIDX_ANNOTATION_VERSION"

  // Android tests
  androidTestImplementation "androidx.test.espresso:espresso-core:$ANDROIDX_ESPRESSO_VERSION"
  androidTestImplementation "androidx.multidex:multidex:$ANDROIDX_MULTIDEX_VERSION"
  testImplementation "org.hamcrest:hamcrest:$HAMCREST_2_VERSION"
}

afterEvaluate {
  android.libraryVariants.all { variant ->
    def name = "${variant.name.capitalize()}"
    task("javadoc${name}", type: Javadoc, dependsOn: "assemble${name}") {
      source = android.sourceSets.main.java.srcDirs
      source += "build/generated/aidl_source_output_dir/${variant.name}/out"
      classpath += project.files(variant.javaCompileProvider.get().classpath.files) // https://stackoverflow.com/a/34572606
      classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
      options.addBooleanOption "Xdoclint:none", true
      options.addBooleanOption "-frames", true
      options.links 'https://developer.android.com/reference/'
      options.overview 'overview.html'
      destinationDir = file("build/docs/javadoc/${variant.name}/")
    }
  }
}

ext{
  project_name = 'Clover Android SDK'
  project_description = 'Android services and objects used for interacting with Clover Services'
}


